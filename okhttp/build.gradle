apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'git-repo'

description = 'OkHttp'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    maven { url "${System.env.HOME}/.m2/repository" }
    github("layerhq", "maven-private", "master", "releases")
    mavenCentral()
}

dependencies {
    compile group: 'com.squareup.okio', name: 'okio', version:'0.9.0.a71ef0da.LAYER'
}


/*************************************************
 * Uploading
 * e.g. > gradle clean build publish
 ************************************************/

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task testJar(type: Jar) {
    from sourceSets.test.output
    exclude "**/*Test*"
}

task testSourceJar(type: Jar) {
    from sourceSets.test.allJava
}

public String getLocalGit() {
    return "$rootProject.ext.gitRepoHome/$rootProject.ext.org/$rootProject.ext.repo"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'okhttp'
            version = rootProject.version
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact testJar {
                classifier "test"
            }

            artifact testSourceJar {
                classifier "testsources"
            }
        }
    }
    repositories {
        maven {
            url "file://${getLocalGit()}/releases"
        }
    }
}

task cleanLocalGit(type: Delete) {
    doFirst {
        println "Deleting local git repo `${getLocalGit()}`..."
        delete "${getLocalGit()}"
    }
}
